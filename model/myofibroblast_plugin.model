group { 
  #Author = Sanchez et al.;
  #Year = 2019;
  #Title = Heterogeneous Effects of Fibroblast-Myocyte Coupling in Different Regions of the Human Atria Under Conditions of Atrial Fibrillation;
  #Journal = Front. Physiol. 10 pp. 847;
  #DOI = 10.3389/fphys.2019.00847 ;
  #Comments = Including sodium current and RMP of -26 mV;
} .reference();

Vm; .external(); .nodal();
//Vfb;  .lookup(-100, 100, 0.005);
Iion; .external(); .nodal(); Iion=Iion;
dt; .units(s);

// Numerical parameters

T = 306.15;   // kelvin (in membrane)
F = 96487.0;   // coulomb_per_mole (in membrane)
R = 8314.0;   // millijoule_per_mole_kelvin (in membrane)

// Temperature dependencies
q10exp = (T - 306.15)/10;
q10PNa = 1.45; // Milburn et al. 1995
q10IK1 = 1.4;
q10NKA = 1.63; // Grandi et al. 2011
q10tauNa = 3;

// Fibroblast parameters

group {
g_Kv = 0.37*Cm;
g_K1 = 0.25*Cm;
g_NKA = 5.39*Cm; /*5.39*/
g_Nab = 0.0217*Cm;
rkv_mod = 0; 
skv_mod = 0;

g_Ca_L = 6.75; .units(nS);
K_NaCa = 0.0374842; .units(pA*L^4/mmol^4);
g_B_Ca = 0.078681; .units(nS);

//INa_Poulet
P_Na = 0.0000225; 
Na_h_Vhalf = 64.7;
Na_h_hill = 6.9;
Na_m_Vhalf = 29.9;
Na_m_hill = -3.1;


Cm = 6.3;//pF

G_gj = 0.5; .units(nS);//.units(nS/cm^2);
num_fb = 1;
fb_surf = Cm*100.; .units(um^2);
fb_myo_surf_ratio = Cm/185.;
} .param();

K_o = 5.3581;
Na_o = 130.0110;
Ca_o = 1.8; //Nygren
Ca_c = Ca_o; //Nygren
Mg_i = 2.5;                /* mM      */

Vol_i = 0.00137;

B = -200.0;
V_rev = -150.0;
K_mK = 1.0;
K_mNa = 11.0;

Eca_app = 60.0; // mV



i_CaP_max = 4; .units(pA);
E_Ca_app = 60; .units(mV);
k_srca = 0.5; .units(mmol/L);
Vol_d = 0.00011768; .units(nL);
k_rel_i = 0.0003; .units(mmol/L);
gamma_Na = 0.45; .units(unitless);
k_CaP = 0.0002; .units(mmol/L);
d_NaCa = 0.0003; .units(L^4/mmol^4);
k_Ca = 0.025; .units(mmol/L);

tau_tr = 0.01; .units(s);
Vol_up = 0.0003969; .units(nL);
k_cyca = 0.0003; .units(mmol/L);
k_rel_d = 0.003; .units(mmol/L);
alpha_rel = 200000; .units(pA*L/mmol);
r_recov = 0.815; .units(unitless/s);
I_up_max = 2800; .units(pA);
tau_di = 0.01; .units(s);
k_xcs = 0.4; .units(unitless);
Vol_rel = 0.0000441; .units(nL);

// Initial Conditions
Vfb_init = -26.7096;
K_i_init = 124.4059; 
Na_i_init = 13.3789; 
m_init = 0.3163;
h1_init = 0.0157;
h2_init = 0.0157;
r_init = 0.2614;
s_init = 0.7655;
//
Ca_i_init = 6.5e-5; .units(mmol/L);
O_C_init = 0.026766; .units(unitless);
O_TC_init = 0.012922; .units(unitless);
O_TMgC_init = 0.190369; .units(unitless);
O_TMgMg_init = 0.714463; .units(unitless);
//O_init = 1.38222; .units(unitless);
Ca_rel_init = 0.632613; .units(mmol/L);
Ca_up_init = 0.649195; .units(mmol/L);
O_Calse_init = 0.431547; .units(unitless);
F1_init = 0.470055; .units(unitless);
F2_init = 0.002814; .units(unitless);
f_L1_init = 0.998597; .units(unitless);
f_L2_init = 0.998586; .units(unitless);
Ca_d_init = 7.1e-5; .units(mmol/L);

// Numeric methods
group{
	m; h1; h2; 
	r; s;
}.method(rush_larsen);

/*group {
    K_i; Na_i;
}.method(fe);*/

// Analytical equations for the fibroblast

// Nernst Potentials
E_K = R*T/F * log(K_o/K_i);
E_Na = R*T/F * log(Na_o/Na_i);
E_Ca = R*T / (2.0 * F) * log(Ca_c / Ca_i);

// INa
m_inf = 1/(1 + exp((Vfb + Na_m_Vhalf - (4.3 + 5.0)/20*(T - 294.15))/Na_m_hill));
h_inf = 1/(1 + exp((Vfb + Na_h_Vhalf - (4.7 + 11)/20*(T - 294.15))/Na_h_hill));
tau_m = 1.0/pow(q10tauNa,q10exp) * (0.000042*exp( -1.0*pow(((Vfb+25.57)/28.8),2) ) + 0.000024); .units(s);
tau_h1 = 1.0/pow(q10tauNa,q10exp) * (0.03/(1+exp((Vfb+35.1)/3.2)) + 0.0003);
tau_h2 = 1.0/pow(q10tauNa,q10exp) * (0.12/(1+exp((Vfb+35.1)/3.2)) + 0.003);

// Time and voltage dependent K+ current, IKv
tau_r = 0.0203 + 0.1380*exp(-((Vfb + 20)/25.9)*((Vfb + 20)/25.9));
tau_s = 1.574 + 5.268*exp(-((Vfb + 23)/22.7)*((Vfb + 23)/22.7));
r_inf = 1/(1 + exp(-(Vfb + rkv_mod)/11));
s_inf = 1/(1 + exp((Vfb + skv_mod)/7));

// INa
INa = pow(q10PNa,q10exp) * P_Na * pow(m,3) * (0.9*h1 + 0.1*h2) 
* Na_o * Vfb * pow(F,2)/(R*T) * (exp((Vfb-E_Na)*F/R/T) - 1) / (exp(Vfb*F/R/T) - 1);

// Time and voltage dependent K+ current, IKv
IKv = g_Kv*r*s*(Vfb - E_K);

// Inward-rectifier K+ current, IK1
IK1 = pow(q10IK1,q10exp) * g_K1 * pow(K_o,0.4457) * (Vfb - E_K) 
/ (1 + exp(1.5*(Vfb - E_K + 3.6)*F/R/T));

// Na+-K+ ATPase current, INKA
INKA = pow(q10NKA,q10exp) * g_NKA*(K_o/(K_o + K_mK)) * ((pow(Na_i, 1.5))/((pow(Na_i, 1.5)) 
+ (pow(K_mNa,1.5))))*(Vfb - V_rev)/(Vfb - B);

// Na+ background current
INab = g_Nab*(Vfb - E_Na);

// Intracellular Ion Concentrations
diff_K_i = -(IK1 + IKv - 2*INKA )/(Vol_i*F); 
diff_Na_i = -(INa + INab + 3*INKA )/(Vol_i*F); 

// INa
//diff_m = (m_inf - m)/tau_m;
diff_h1 = (h_inf - h1)/tau_h1;
diff_h2 = (h_inf - h2)/tau_h2;

// Time and voltage dependent K+ current, IKv
//diff_r = (r_inf - r)/tau_r;
//diff_s = (s_inf - s)/tau_s;

//L_type_Ca_channel
f_Ca = (Ca_d/(Ca_d+k_Ca)); .units(unitless);
i_Ca_L = (g_Ca_L*d_L*((f_Ca*f_L1)+((1. - f_Ca)*f_L2))*(Vfb - E_Ca_app)); .units(pA);

//L_type_Ca_channel_d_L_gate
d_L_infinity = (1./(1.+exp(((Vfb+9.)/-5.8)))); .units(unitless);
d_L_factor = ((Vfb+35.)/30.); .units(unitless);
tau_d_L = ((0.0027*exp((-d_L_factor*d_L_factor)))+0.002); .units(s);

//L_type_Ca_channel_f_L1_gate
diff_f_L1 = ((f_L_infinity - f_L1)/tau_f_L1); .units(unitless/s);
f_L_infinity = (1./(1.+exp(((Vfb+27.4)/7.1)))); .units(unitless);
f_L_factor = (Vfb+40.); .units(mV);
tau_f_L1 = ((0.161*exp((((-f_L_factor*f_L_factor)/14.4)/14.4)))+0.01); .units(s);

//L_type_Ca_channel_f_L2_gate
diff_f_L2 = ((f_L_infinity - f_L2)/tau_f_L2); .units(unitless/s);
tau_f_L2 = ((1.3323*exp((((-f_L_factor*f_L_factor)/14.2)/14.2)))+0.0626); .units(s);

//Ca_independent_transient_outward_K_current
i_t = (g_t*r*s*(Vfb - E_K)); .units(pA);

//Ca_independent_transient_outward_K_current_r_gate
tau_rr = ((0.0035*exp((((-Vfb*Vfb)/30.)/30.)))+0.0015); .units(s);
rr_infinity = (1./(1.+exp(((Vfb - 1.)/-11.)))); .units(unitless);

//Ca_independent_transient_outward_K_current_s_gate
tau_ss = ((0.025635*exp((-s_factor*s_factor)))+0.01414); .units(s);
ss_infinity = (1./(1.+exp(((Vfb+40.5)/11.5)))); .units(unitless);
s_factor = ((Vfb+52.45)/15.8827); .units(unitless);

//background_currents
i_B_Ca = (g_B_Ca*(Vfb - E_Ca)); .units(pA);

//sarcolemmal_calcium_pump_current
i_CaP = ((i_CaP_max*Ca_i)/(Ca_i+k_CaP)); .units(pA);

//Na_Ca_ion_exchanger_current
i_NaCa = ((K_NaCa*((Na_i*Na_i*Na_i*Ca_c*exp(((F*Vfb*gamma_Na)/(R*T)))) - (Na_o*Na_o*Na_o*Ca_i*exp((((gamma_Na - 1.)*Vfb*F)/(R*T))))))/(1.+(d_NaCa*((Na_o*Na_o*Na_o*Ca_i)+(Na_i*Na_i*Na_i*Ca_c))))); .units(pA);

//intracellular_ion_concentrations
diff_Ca_d = (-(i_Ca_L+i_di)/(2.*Vol_d*F)); .units(mmol/s/L);
diff_Ca_i = ((-((i_B_Ca+i_CaP+i_up) - (i_di+i_rel+(2.*i_NaCa)))/(2.*Vol_i*F)) - (1.*J_O)); .units(mmol/s/L);
i_di = (((Ca_d - Ca_i)*2.*Vol_d*F)/tau_di); .units(pA);

//intracellular_Ca_buffering
diff_O_TMgC = J_O_TMgC; .units(unitless/s);
diff_O_TC = J_O_TC; .units(unitless/s);
diff_O_C = J_O_C; .units(unitless/s);
J_O_TMgC = ((200000.*Ca_i*((1. - O_TMgC) - O_TMgMg)) - (6.6*O_TMgC)); .units(unitless/s);
J_O_TC = ((78400.*Ca_i*(1. - O_TC)) - (392.*O_TC)); .units(unitless/s);
J_O = ((0.08*J_O_TC)+(0.16*J_O_TMgC)+(0.045*J_O_C)); .units(unitless/s);
//diff_O = J_O; .units(unitless/s);
J_O_C = ((200000.*Ca_i*(1. - O_C)) - (476.*O_C)); .units(unitless/s);
J_O_TMgMg = ((2000.*Mg_i*((1. - O_TMgC) - O_TMgMg)) - (666.*O_TMgMg)); .units(unitless/s);
diff_O_TMgMg = J_O_TMgMg; .units(unitless/s);

//Ca_handling_by_the_SR
r_Ca_d_term = (Ca_d/(Ca_d+k_rel_d)); .units(unitless);
diff_F2 = ((r_act*F1) - (r_inact*F2)); .units(unitless/s);
diff_F1 = ((r_recov*((1. - F1) - F2)) - (r_act*F1)); .units(unitless/s);
r_Ca_i_factor = (r_Ca_i_term*r_Ca_i_term*r_Ca_i_term*r_Ca_i_term); .units(unitless);
diff_Ca_up = ((i_up - i_tr)/(2.*Vol_up*F)); .units(mmol/s/L);
r_Ca_d_factor = (r_Ca_d_term*r_Ca_d_term*r_Ca_d_term*r_Ca_d_term); .units(unitless);
diff_O_Calse = J_O_Calse; .units(unitless/s);
i_rel = (alpha_rel*i_rel_factor*(Ca_rel - Ca_i)); .units(pA);
r_act = (203.8*(r_Ca_i_factor+r_Ca_d_factor)); .units(unitless/s);
diff_Ca_rel = (((i_tr - i_rel)/(2.*Vol_rel*F)) - (31.*J_O_Calse)); .units(mmol/s/L);
i_rel_factor = (i_rel_f2*i_rel_f2); .units(unitless);
J_O_Calse = ((480.*Ca_rel*(1. - O_Calse)) - (400.*O_Calse)); .units(unitless/s);
r_inact = (33.96+(339.6*r_Ca_i_factor)); .units(unitless/s);
i_rel_f2 = (F2/(F2+0.25)); .units(unitless);
r_Ca_i_term = (Ca_i/(Ca_i+k_rel_i)); .units(unitless);
i_tr = (((Ca_up - Ca_rel)*2.*Vol_rel*F)/tau_tr); .units(pA);
i_up = ((I_up_max*((Ca_i/k_cyca) - ((k_xcs*k_xcs*Ca_up)/k_srca)))/(((Ca_i+k_cyca)/k_cyca)+((k_xcs*(Ca_up+k_srca))/k_srca))); .units(pA);

//myocyte coupling
I_gj = G_gj*(Vm-Vfb); .units(pA);
I_fb = INa + IKv + IK1 + INab + INKA + i_CaP + i_NaCa + i_Ca_L + i_B_Ca - I_gj; //.units(pA/pF);
diff_Vfb = -1000*I_fb/Cm;

Iion += I_gj*num_fb*fb_myo_surf_ratio;


group {
	Vfb;
  I_gj;
  I_fb;
	m; h1; h2;
	r; s;
  K_i; Na_i;
  Ca_i;
  IKv; IK1; INa; INKA; i_Ca_L; i_NaCa;
} .trace();
